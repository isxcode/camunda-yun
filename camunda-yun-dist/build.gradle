// 构建lib文件夹
tasks.register('build_agent_lib', Copy) {

    dependsOn(":camunda-yun-agent:bootJar")
    dependsOn(":camunda-yun-backend:camunda-yun-api:jar")

    def dependencies = configurations.runtimeClasspath.findAll {
        it.name.contains('fastjson2') ||
                it.name.contains('fastjson') ||
                it.name.contains('camunda-yun-api') ||
                it.name.contains('log4j-api') ||
                it.name.contains('spark-core') ||
                it.name.contains('jetty-server') ||
                it.name.contains('jetty-util') ||
                it.name.contains('jetty-http') ||
                it.name.contains('jetty-io')
    }
    dependencies.each { dependency ->
        from dependency
        into 'build/zhishuyun_agent/lib'
    }

    from rootDir.getAbsolutePath() + '/camunda-yun-backend/camunda-yun-api/build/libs'
    from rootDir.getAbsolutePath() + '/camunda-yun-agent/build/libs'
    from rootDir.getAbsolutePath() + '/resources/jdbc/system'
    into 'build/zhishuyun-agent/lib'
}

// 构建plugins文件夹
tasks.register('build_agent_plugins', Copy) {

    dependsOn(':camunda-yun-plugins:camunda-query-sql-plugin:jar')
    dependsOn(':camunda-yun-plugins:camunda-data-sync-jdbc-plugin:jar')
    dependsOn(':camunda-yun-plugins:camunda-excel-sync-jdbc-plugin:jar')
    dependsOn(':camunda-yun-vip:camunda-yun-plugins:camunda-container-sql-plugin:jar')
    dependsOn(':camunda-yun-vip:camunda-yun-plugins:camunda-real-sync-plugin:jar')

    from rootDir.getAbsolutePath() + '/camunda-yun-plugins/camunda-query-sql-plugin/build/libs'
    from rootDir.getAbsolutePath() + '/camunda-yun-plugins/camunda-data-sync-jdbc-plugin/build/libs'
    from rootDir.getAbsolutePath() + '/camunda-yun-plugins/camunda-excel-sync-jdbc-plugin/build/libs'
    from rootDir.getAbsolutePath() + '/camunda-yun-vip/camunda-yun-plugins/camunda-container-sql-plugin/build/libs'
    from rootDir.getAbsolutePath() + '/camunda-yun-vip/camunda-yun-plugins/camunda-real-sync-plugin/build/libs'
    into 'build/zhishuyun-agent/plugins'
}

// 构建至数云代理
tasks.register('build_agent', Tar) {

    mustRunAfter(":camunda-yun-frontend:make")

    dependsOn('build_agent_lib')
    dependsOn('build_agent_plugins')

    compression = Compression.GZIP
    archiveFileName = 'zhishuyun-agent.tar.gz'

    from('zhishuyun-agent/bin') {
        into 'zhishuyun-agent/bin'
    }
    from(rootDir.getAbsolutePath() + "/camunda-yun-agent/src/main/resources/application.yml") {
        into 'zhishuyun-agent/conf'
    }
    from('zhishuyun-agent/logs') {
        into 'zhishuyun-agent/logs'
    }
    from('build/zhishuyun-agent/plugins') {
        into 'zhishuyun-agent/plugins'
    }
    from('zhishuyun-agent/works') {
        into 'zhishuyun-agent/works'
    }
    from('zhishuyun-agent/file') {
        into 'zhishuyun-agent/file'
    }
    from('build/zhishuyun-agent/lib') {
        into 'zhishuyun-agent/lib'
    }
    from('../README.md') {
        into 'zhishuyun-agent/'
    }
}

// 构建至数云
tasks.register('build_zhishuyun', Tar) {

    mustRunAfter(":camunda-yun-backend:make")

    compression = Compression.GZIP
    archiveFileName = 'zhishuyun.tar.gz'

    from('zhishuyun/bin') {
        into 'zhishuyun/bin'
    }
    from('zhishuyun/conf') {
        into 'zhishuyun/conf'
    }
    from(rootDir.getAbsolutePath() + '/camunda-yun-backend/camunda-yun-main/src/main/resources/application-local.yml') {
        into 'zhishuyun/conf'
    }
    from(rootDir.getAbsolutePath() + '/resources/jdbc/system') {
        into 'zhishuyun/resources/jdbc/system'
    }
    from(rootDir.getAbsolutePath() + '/camunda-yun-backend/camunda-yun-main/build/libs/zhishuyun.jar') {
        into 'zhishuyun/lib'
    }
    from(rootDir.getAbsolutePath() + '/README.md') {
        into 'zhishuyun/'
    }
}

// 打包
tasks.register('make', GradleBuild) {

    tasks = [":camunda-yun-frontend:make", "build_agent", ":camunda-yun-backend:make", "build_zhishuyun"]
}

// 添加依赖
dependencies {

    implementation 'com.sparkjava:spark-core:2.9.4'

    implementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.51.v20230217'
}